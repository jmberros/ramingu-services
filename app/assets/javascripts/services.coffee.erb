$ ->
  animateAllTheThings()
  configureFeatures()
  configurePopups()
  lazyLoadImages()
  setupAJAXForm()

  # Tooltips!
  $('[data-toggle="tooltip"]').tooltip()

  # Mail
  $('#mail').text('copy' + 'lingual' + '@' + 'gmai' + 'l.co' + 'm')

animateAllTheThings = ->
  $('#copylingual').find('.fa-arrow-down').addClass('pulse animated infinite')

  new WOW().init()

lazyLoadImages = ->
  $('.lazy-load').each ->
    src = this.getAttribute('data-src')
    $(this).hide().attr('src', src).fadeIn(1000)

configureFeatures = ->
  # Spy scroll and activate current section in the topbar
  $('body').scrollspy target: '.navbar-fixed-top'

  # Smooth scrolling for local links
  #smoothScroll.init(
    #offset: 50 # Height of the topbar
    #speed: 400
    #easing: 'easeInOutCubic'
    #updateURL: yes
  #)

configurePopups = ->
  # Pop-up modal galleries
  $('.popup-gallery').each( ->
    $(this).magnificPopup(
      preloader: yes
      delegate: 'a'
      type: 'image'
      titleSrc: 'title'
      midClick: yes
      gallery: { enabled: yes }
    )
  )

  # Ajax-popup of blog links
  $('.blog-link').magnificPopup(
    type: 'iframe'
    callbacks:
      open: (item) ->
        $('.mfp-content').addClass('big-modal')
  )

setupAJAXForm = ->
  $( '#contact-form' ).on('click', 'button', (event) ->
    event.preventDefault()
    $form = $(this).closest( 'form' )
    $alert = $form.siblings( '.alert' )
    $alert.removeClass( 'shake bounce animated' )

    contact_data =
      name: $form.find( '#name' ).val()
      email: $form.find( '#email' ).val()
      reason: $form.find( '#contact_reason:selected' ).val()
      message: $form.find( '#message' ).val()
      authenticity_token: $form.find( '[name=authenticity_token]' ).val()

    $form.find( 'button' )
      .attr( 'disabled', true )
      .addClass( 'flash animated infinite' )

    $.post( $form.attr( 'action' ), contact_data )
      .done( ( response ) ->
        $alert.removeClass( 'alert-danger' )
          .addClass( 'alert-success' )
          .text( "Message Sent!" )

        if $alert.is( ':visible' )
          $alert.addClass('bounce animated')
        else
          $alert.slideDown( 250 )

        $alert.delay( 2500 ).slideUp( 250 )
      )
      .fail( (jqXHR, status, error) ->
        $alert.removeClass( 'alert-success' ).addClass( 'alert-danger' )

        if $form.find( '#name' ).val().length && $form.find('#email').val().length
          $alert.text( "Oops, something went wrong. You can still write me at copylingual@gmail.com" )
        else
          $alert.text( "Please write your name and e-mail." )

        if $alert.is( ':visible' )
          $alert.addClass( 'shake animated' )
        else
          $alert.slideDown( 250 )
      )
      .always( ->
        $form.find( 'button' )
          .attr( 'disabled', false )
          .removeClass( 'flash animated infinite' )
      )
  )
